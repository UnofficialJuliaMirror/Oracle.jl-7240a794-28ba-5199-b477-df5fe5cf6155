
const PREFIX = joinpath(@__DIR__, "usr")
const DOWNLOADS = joinpath(PREFIX, "downloads")
#const INSTANT_CLIENT_DOWNLOAD_FILE = "instantclient-basic-linux.x64-18.3.0.0.0dbru.zip"
#const INSTANT_CLIENT_DOWNLOAD_URL = "https://felipenoris.s3.nl-ams.scw.cloud/$INSTANT_CLIENT_DOWNLOAD_FILE"
#const INSTANT_CLIENT_LOCAL_FILEPATH = joinpath(DOWNLOADS, INSTANT_CLIENT_DOWNLOAD_FILE)

const ODPI_SOURCE_URL = "https://github.com/oracle/odpi/archive/v3.0.0.tar.gz"
const ODPI_SOURCE_LOCAL_FILEPATH = joinpath(DOWNLOADS, "odpi_source.tar.gz")

@static if Sys.islinux()
    const SHARED_LIB = joinpath(PREFIX, "lib", "libdpi.so.3.0.0")
elseif Sys.isapple()
    const SHARED_LIB = joinpath(PREFIX, "lib", "libdpi.3.0.0.dylib")
else
    error("Target system not supported.")
end

mkdir_if_not_exists(dir) = !isdir(dir) && mkdir(dir)

function download_source_files()
    mkdir_if_not_exists(PREFIX)
    mkdir_if_not_exists(DOWNLOADS)

#=
    if !isfile(INSTANT_CLIENT_LOCAL_FILEPATH)
        download(INSTANT_CLIENT_DOWNLOAD_URL, INSTANT_CLIENT_LOCAL_FILEPATH)
        @assert isfile(INSTANT_CLIENT_LOCAL_FILEPATH)
    end
=#

    if !isfile(ODPI_SOURCE_LOCAL_FILEPATH)
        download(ODPI_SOURCE_URL, ODPI_SOURCE_LOCAL_FILEPATH)
    end
end

function build_shared_library(; verbose::Bool=false)
    download_source_files()

    src_dir = joinpath(PREFIX, "src")
    mkdir_if_not_exists(src_dir)

    lib_dir = joinpath(PREFIX, "lib")
    mkdir_if_not_exists(lib_dir)


    @static if Sys.islinux()
        #=
        tar -xf $ODPI_SOURCE_LOCAL_FILEPATH -C $src_dir --strip-components=1
        cc -c -fPIC -I ../include -ldl -o dpi.o dpi.c
        cc -shared -fPIC -Wl,-soname,libdpi.so.3 -o ../lib/libdpi.so.3.0.0 dpi.o -lc
        =#

        build_script = [
            ["tar", "-xf", ODPI_SOURCE_LOCAL_FILEPATH, "-C", src_dir, "--strip-components=1"],
            ["cc", "-c", "-fPIC", "-I", joinpath(src_dir, "include"), "-ldl", "-o", joinpath(src_dir, "embed", "dpi.o"), joinpath(src_dir, "embed", "dpi.c")],
            ["cc", "-shared", "-fPIC", "-Wl,-soname,libdpi.so.3", "-o", SHARED_LIB, joinpath(src_dir, "embed", "dpi.o"), "-lc"]
        ]

    elseif Sys.isapple()
        #=
        tar -xf $ODPI_SOURCE_LOCAL_FILEPATH -C $src_dir --strip-components=1
        cc -dynamiclib -I ../include -o ../lib/libdpi.dylib dpi.c
        =#

        build_script = [
            ["tar", "-xf", ODPI_SOURCE_LOCAL_FILEPATH, "-C", src_dir, "--strip-components=1"],
            ["cc", "-dynamiclib", "-I", joinpath(src_dir, "include"), "-o", SHARED_LIB, joinpath(src_dir, "embed", "dpi.c")]
        ]

    else
        error("Target system not supported.")
    end

    for cmd_array in build_script
        actual_cmd = Cmd(cmd_array)
        verbose && println(actual_cmd)
        run(Cmd(actual_cmd, dir=PREFIX))
    end

    @assert isfile(SHARED_LIB) "Failed building libdpi shared library."
    rm(src_dir, recursive=true)
end

function write_deps_file()
    @assert isfile(SHARED_LIB) "Couldn't find shared library $SHARED_LIB."
    lib_file = basename(SHARED_LIB)

    deps_file_content = """
# This file is generated by build.jl and should be called
# from `check_deps()` from within your module's `__init__()` method
if isdefined((@static VERSION < v"0.7.0-DEV.484" ? current_module() : @__MODULE__), :Compat)
    import Compat.Libdl
elseif VERSION >= v"0.7.0-DEV.3382"
    import Libdl
end
const libdpi = joinpath(@__DIR__, "usr", "lib", "$lib_file")
function check_deps()
    global libdpi
    if !isfile(libdpi)
        error("\$(libdpi) does not exist, Please re-run Pkg.build(\\"Oracle\\"), and restart Julia.")
    end

    if Libdl.dlopen_e(libdpi) in (C_NULL, nothing)
        error("\$(libdpi) cannot be opened, Please re-run Pkg.build(\\"Oracle\\"), and restart Julia.")
    end
end
"""

    open(joinpath(@__DIR__, "deps.jl"), "w") do f
        write(f, deps_file_content)
    end
end

function main()
    if !isfile(SHARED_LIB)
        build_shared_library()
    end

    write_deps_file()
end

main()
